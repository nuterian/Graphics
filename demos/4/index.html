<!DOCTYPE html>
<html lang="en">
<head>
   <meta charset="UTF-8">
   
   <title>Even More Ray Tracing | Graphics - Jugal Manjeshwar</title>
   <link href='http://fonts.googleapis.com/css?family=Roboto:400,300,700,400italic' rel='stylesheet' type='text/css'>
   <link rel="stylesheet" type="text/css" href="../../css/normalize.css">
   <link rel="stylesheet" type="text/css" href="../../css/style.css">

</head>
<body class="demo">
   <div class="title row">
      <div class="col"><a href="../../" class="ico-home"></a></div>
      <h1 class="col">Even More Ray Tracing</h1>


   </div>
   <canvas id='mainCanvas' class="canvas"></canvas>

<script type="text/javascript" src="https://code.jquery.com/jquery-1.11.2.min.js"></script>
<script type="text/javascript">
   var _params = [
      {name: 'uShadows', value: 0}
   ];

   $('#shadows1').change(function() {
      _params[0].value = $(this).prop("checked") ? 1 : 0;
   });

</script>   
<script src="lib.js"></script>
<script id='my_vertex_shader' type='x-shader/x-vertex'>
   attribute vec3 aPosition;
   varying   vec3 vPosition;
   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;
   }
</script>

<script id='my_fragment_shader' type='x-shader/x-fragment'>

   precision highp float;
   uniform float uTime;
   uniform vec3  uCursor;
   varying vec3  vPosition;

   #define PLANE 0x000
   #define SHAPE_BOX 0x001
   #define SHAPE_SPHERE 0x002
   #define SHAPE_CYLINDER 0x003

   struct Shape {
      vec4 p;
      vec3 s;
      vec4 m;
      float ref;
      int type;
   };

   const int numLights = 2, numShapes = 6;
   Shape shapes[numShapes];
   vec4 material;

   vec4 ss, mm;
   int tt;
   float rr;

   float minT, minTOut;

   vec3 Lrgb[numLights];
   vec3 Ldir[numLights];

   const float PI = 3.1415926535897932384626433832795;

   vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
   vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
   vec4 permute(vec4 x) { return mod289(((x*34.0)+1.0)*x); }
   vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
   vec3 fade(vec3 t) { return t*t*t*(t*(t*6.0-15.0)+10.0); }

   float noise(vec3 P) {
      vec3 i0 = mod289(floor(P)), i1 = mod289(i0 + vec3(1.0));
      vec3 f0 = fract(P), f1 = f0 - vec3(1.0), f = fade(f0);
      vec4 ix = vec4(i0.x, i1.x, i0.x, i1.x), iy = vec4(i0.yy, i1.yy);
      vec4 iz0 = i0.zzzz, iz1 = i1.zzzz;
      vec4 ixy = permute(permute(ix) + iy), ixy0 = permute(ixy + iz0), ixy1 = permute(ixy + iz1);
      vec4 gx0 = ixy0 * (1.0 / 7.0), gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
      vec4 gx1 = ixy1 * (1.0 / 7.0), gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
      gx0 = fract(gx0); gx1 = fract(gx1);
      vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0), sz0 = step(gz0, vec4(0.0));
      vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1), sz1 = step(gz1, vec4(0.0));
      gx0 -= sz0 * (step(0.0, gx0) - 0.5); gy0 -= sz0 * (step(0.0, gy0) - 0.5);
      gx1 -= sz1 * (step(0.0, gx1) - 0.5); gy1 -= sz1 * (step(0.0, gy1) - 0.5);
      vec3 g0 = vec3(gx0.x,gy0.x,gz0.x), g1 = vec3(gx0.y,gy0.y,gz0.y),
           g2 = vec3(gx0.z,gy0.z,gz0.z), g3 = vec3(gx0.w,gy0.w,gz0.w),
           g4 = vec3(gx1.x,gy1.x,gz1.x), g5 = vec3(gx1.y,gy1.y,gz1.y),
           g6 = vec3(gx1.z,gy1.z,gz1.z), g7 = vec3(gx1.w,gy1.w,gz1.w);
      vec4 norm0 = taylorInvSqrt(vec4(dot(g0,g0), dot(g2,g2), dot(g1,g1), dot(g3,g3)));
      vec4 norm1 = taylorInvSqrt(vec4(dot(g4,g4), dot(g6,g6), dot(g5,g5), dot(g7,g7)));
      g0 *= norm0.x; g2 *= norm0.y; g1 *= norm0.z; g3 *= norm0.w;
      g4 *= norm1.x; g6 *= norm1.y; g5 *= norm1.z; g7 *= norm1.w;
      vec4 nz = mix(vec4(dot(g0, vec3(f0.x, f0.y, f0.z)), dot(g1, vec3(f1.x, f0.y, f0.z)),
                         dot(g2, vec3(f0.x, f1.y, f0.z)), dot(g3, vec3(f1.x, f1.y, f0.z))),
                    vec4(dot(g4, vec3(f0.x, f0.y, f1.z)), dot(g5, vec3(f1.x, f0.y, f1.z)),
                         dot(g6, vec3(f0.x, f1.y, f1.z)), dot(g7, vec3(f1.x, f1.y, f1.z))), f.z);
      return 2.2 * mix(mix(nz.x,nz.z,f.y), mix(nz.y,nz.w,f.y), f.x);
   }

   float turbulence(vec3 p) { 
       float sum=0.;
       for (int i =0; i<6; i++){
           sum += abs( noise( pow(2.,float(i)) * p ) ) / pow(2.,float(i));
       }
       return sum;
   }

   void setupLights() {
      Lrgb[0] = vec3(1.0, 0.7, 0.2);
      Lrgb[1] = vec3(0.4, 0.7, 1.0);

      Ldir[0] = normalize(vec3(-5., 3., 3.));
      Ldir[1] = normalize(vec3(uCursor.x, uCursor.y, 2.));
   }

   Shape createBox(vec3 pos, vec3 scale, vec4 material, float ref) {
      return Shape(vec4(pos, max(scale.x, max(scale.y, scale.z))), scale, material, ref, SHAPE_BOX);
   }

   Shape createSphere(vec3 pos, float r, vec4 material, float ref) {
      return Shape(vec4(pos, r), vec3(r, r, r), material, ref, SHAPE_SPHERE);
   }

   float raySphere(vec3 V, vec3 W, vec4 s) {
      float b = 2. * dot(V -= s.xyz, W);
      float c = dot(V, V) - s.w * s.w;
      float d = b * b - 4. * c;
      float r1 = (-b - sqrt(d)) / 2.,
         r2 = (-b + sqrt(d)) / 2.;
      return d < 0. ? 1000. : min(r1, r2) >= 0. ? min(r1, r2) : max(r1, r2);
   }

   float rayPlane(vec3 V, vec3 W, vec4 p) {
      return -( dot( p.xyz, V ) + p.w ) / dot( p.xyz, W );
   }

   void findShape(vec3 V, vec3 W) {
      minT = 1000.;

      for(int j = 0; j < numShapes; j++) {

         Shape s = shapes[j];
         float t;

         if(s.type == SHAPE_BOX) {
            vec4 planes[6];
            planes[0] = vec4( 1., 0., 0., -s.s.x - s.p.x);
            planes[1] = vec4(-1., 0., 0., -s.s.x + s.p.x);
            planes[2] = vec4( 0., 1., 0., -s.s.y - s.p.y);
            planes[3] = vec4( 0.,-1., 0., -s.s.y + s.p.y);
            planes[4] = vec4( 0., 0., 1., -s.s.z - s.p.z);
            planes[5] = vec4( 0., 0.,-1., -s.s.z + s.p.z);

            float tIn, tOut, maxTIn = 0., minTOut = 1000.;
            vec4 _ss;
            int miss = 0;

            for( int i = 0; i < 6; i++ ) {
               vec4 plane = planes[i];

               t = rayPlane(V, W, plane);
               float r = dot(plane.xyz, W);

               if( r == 0. ) {
                  if( dot(plane.xyz, V) + plane.w <= 0.) {
                     tIn = 0.;
                     tOut = 1000.;
                  }
                  else {
                     miss = 1;
                  }
                  break;
               }

               tIn = t * (1. - step(0., r));
               tOut = 1000. * (1. - step(0., r)) + t * step(0., r);

               if( tIn > maxTIn ) {
                  _ss = plane;
                  maxTIn = tIn;
               }

               minTOut = min(tOut, minTOut);      
            }

            if ( maxTIn < minTOut && miss == 0 && maxTIn < minT ){
               minT = maxTIn;
               ss = _ss;
               mm = s.m;
               rr = s.ref;
               tt = PLANE;
            }            
         }
         else if(s.type == SHAPE_SPHERE) {
            if( (t = raySphere(V, W, s.p)) > 0. && t < minT ) {
               minT = t;
               ss = s.p;
               mm = s.m;
               rr = s.ref;
               tt = SHAPE_SPHERE;
            }            
         }

      }
   }

   vec3 shadeShape(vec3 W, vec3 S, vec4 s, vec4 m, float ref, int type) {
      vec3 normal, color = m.rgb * .15; ;

      if(type == PLANE) {
         normal = normalize(s.xyz);
      }
      else if(type == SHAPE_SPHERE) {
         normal = (S - s.xyz) / s.w;
         float t = 0.4 * (turbulence(vec3(S.x * 1.5, S.y * 1.5, 10.)) + 1.8);
         float u = pow(0.5 + 0.5 * cos(7. * S.x + 6. * t * 10.), 0.1);
         color = mix(color, vec3(u, u*u, u*u*u), 0.6);
      }

      for( int i = 0; i < numLights; i++ ) {
         float diffuse =  pow( max( 0., dot( Ldir[i], normal ) ), 1.1 ) * 0.5;
         vec3 H = normalize(-W + Ldir[i]);
         float specular = pow( max( 0., dot( normal, H ) ), 3. * m.a);
         color += Lrgb[i] * ( m.rgb * diffuse + min(1., m.a / 20.) * specular );

      }
      return color;
   }

   vec3 rayTrace(vec3 V, vec3 W) {
      findShape(V, W);
      if( minT < 1000. ) {
         return shadeShape(W, V + minT * W, ss, mm, rr, tt);   
      }
      return vec3(0., 0., 0.);
   }

   void main() {
      setupLights();

      shapes[0] = createBox(
         vec3(2.5* sin(uTime), 2.5*cos(uTime), 1.), 
         vec3(1.5, .5, .5), 
         vec4( 29./255., 204./255., 163./255., 1.), 
         1.);

      shapes[1] = createBox(
         vec3(-2.5*cos(uTime), 2.5 * sin(uTime), 3.), 
         vec3(.8, .8, .8), 
         vec4(242./255.,  34./255.,  72./255., 1.), 
         1.);

      shapes[2] = createBox(
         vec3(2.5*cos(uTime), -2.5 * sin(uTime), 3.), 
         vec3(.5, 1.5, .5),  
         vec4(255./255.,  214./255.,  51./255., 1.), 
         1.);

      shapes[3] = createBox(
         vec3(0., -3., 0.), 
         vec3(4., 1., 4.), 
         vec4( 77./255.,  97./255., 255./255., 1.), 
         1.);

      shapes[4] = createBox(
         vec3(4., 0., 0.), 
         vec3(.02, 4., 4.), 
         vec4(240./255., 245./255., 245./255.,  1.), 
         1.5);

      shapes[5] = createBox(
         vec3(0.,  0., -4.), 
         vec3(4.,  4., .02), 
         vec4(240./255., 245./255., 245./255.,  1.), 
         1.);


      vec3 W = normalize(vec3(vPosition.x, vPosition.y, -3.5));
      gl_FragColor = vec4(sqrt(rayTrace(vec3(0., 0., 20.), W)), 1.);
   }

</script>

<script>
start_gl('mainCanvas', document.getElementById('my_vertex_shader'  ).innerHTML,
                    document.getElementById('my_fragment_shader').innerHTML);
</script>

</body>
</html>