<!DOCTYPE html>
<html lang="en">
<head>
   <meta charset="UTF-8">
   
   <title>Introduction to Ray Tracing | Graphics - Jugal Manjeshwar</title>
   <link rel="stylesheet" type="text/css" href="../../css/normalize.css">
   <link rel="stylesheet" type="text/css" href="../../css/style.css">

</head>
<body class="demo">
   <div class="title row">
      <div class="col"><a href="../../" class="ico-home"></a></div>
      <h1 class="col">Introduction to Ray Tracing</h1>
   </div>
   <canvas id='mainCanvas' class="canvas"></canvas>
   
<script src="../../js/lib.js"></script>
<script id='my_vertex_shader' type='x-shader/x-vertex'>
   attribute vec3 aPosition;
   varying   vec3 vPosition;
   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;
   }
</script>

<script id='my_fragment_shader' type='x-shader/x-fragment'>
   precision highp float;
   uniform float uTime;
   uniform vec3  uCursor;
   varying vec3  vPosition;

   const int numSpheres = 6, numLights = 2;
   vec4 sphere[numSpheres];
   vec3 material[numSpheres];

   vec3 Lrgb[numLights];
   vec3 Ldir[numLights];

   const float PI = 3.1415926535897932384626433832795;
   const float OBSCURED = 10000.;

   vec4  visibleSphere;
   vec3  visibleMaterial;
   float visibleT;

   float snoise(vec3 uv, float res) // by trisomie21
   {
      const vec3 s = vec3(1e0, 1e2, 1e4);
      
      uv *= res;
      
      vec3 uv0 = floor(mod(uv, res))*s;
      vec3 uv1 = floor(mod(uv+vec3(1.), res))*s;
      
      vec3 f = fract(uv); f = f*f*(3.0-2.0*f);
      
      vec4 v = vec4(uv0.x+uv0.y+uv0.z, uv1.x+uv0.y+uv0.z,
                    uv0.x+uv1.y+uv0.z, uv1.x+uv1.y+uv0.z);
      
      vec4 r = fract(sin(v*1e-3)*1e5);
      float r0 = mix(mix(r.x, r.y, f.x), mix(r.z, r.w, f.x), f.y);
      
      r = fract(sin((v + uv1.z - uv0.z)*1e-3)*1e5);
      float r1 = mix(mix(r.x, r.y, f.x), mix(r.z, r.w, f.x), f.y);
      
      return mix(r0, r1, f.z)*2.-1.;
   }

   void initLights(vec2 c) {
      Lrgb[0] = vec3(0.7, 0.7, 1.0);
      Lrgb[1] = vec3(1., 0.4, 0.1);

      Ldir[0] = normalize(vec3(0.8 * cos(uTime * 0.6), 0.8 * sin(uTime *0.6), -0.9 ));
      Ldir[1] = normalize(vec3(c.x, c.y, 1. - 2. * dot(c, c)));      
   }

   void initSpheres() {
      material[0] = vec3(255./255., 195./255., 54./255.);
      material[1] = vec3(29./255., 204./255., 163./255.);
      material[2] = vec3(242./255., 34./255., 72./255.);
      material[3] = vec3(77./255., 97./255., 255./255.);
      material[4] = vec3(52./255., 73./255., 94./255.);
      material[5] = vec3(236./255., 240./255., 241./255.);

      sphere[0] = vec4(0., 0., 1.5, 0.15);
      for (int i = 1 ; i < numSpheres ; i++) {
         sphere[i] = vec4(cos(float(i) * PI / 5.) * .3 * cos(uTime + float(i) * PI / 2.), sin(float(i) * PI / 5.) * .3 * cos(uTime + float(i) * PI / 2.), 1.5 + 1. * sin(uTime + float(i) * PI / 2.), .01 * float(i));   
      }
   }

   void raySpheres(vec3 V, vec3 W) {
      visibleT = OBSCURED;

      for (int i = 0; i < numSpheres; i++) {
         vec4 S = sphere[i];

         float b = 2.*dot(W, V-S.xyz);
         float c = dot(V-S.xyz, V-S.xyz) - pow(S.w, 2.0);

         float dd = pow(b, 2.0) - 4. * c;
         if(dd < 0.) { continue; }

         float t = (sqrt(dd) - b) / 2.;

         if (t >= 0. && t < visibleT) {
           visibleT = t;
           visibleSphere = S;
           visibleMaterial = material[i];
         }
      }
   }

   vec3 shadeSphere(vec3 point, vec4 sphere, vec3 material, vec3 W) {
      vec3 color = vec3(0.,0.,0.);

      vec3 ambient = material / 5.;
      vec3 diffuse = material / 1.;
      vec3 lightsum = vec3(0.0,0.0,0.0);
      vec3 normal = ( point - sphere.xyz ) / sphere.w;

      for ( int k = 0; k < numLights; k++ ){
         lightsum += Lrgb[k] * diffuse * max(0., dot(normal, Ldir[k]));
      } 

      color = ambient + lightsum;
      float diff = 1. - abs(snoise(point + 0.2 * vec3(  uTime * 0.05, 0., uTime * 0.05 ), 200. ) );
      diff = min(0.9, max(0.75, diff));
      color *= diff;

      return color;
   }

   void main(void) {
      vec2 c = uCursor.xy;
      initLights(c);

      vec3 V = vec3(0., 0., -1.);
      vec3 W = normalize( vec3(vPosition.x, vPosition.y, 5.0) );

      initSpheres();
      raySpheres(V, W);

      vec3 color = vec3(0., 0., 0.);
      if (visibleT == OBSCURED) {
         gl_FragColor = vec4(sqrt(color), 1.); // Do Gamma correction.
      }
      else {
         color = shadeSphere(V + visibleT * W, visibleSphere, visibleMaterial, W);
         gl_FragColor = vec4(sqrt(color), 1.); // Do Gamma correction.      
      }
   }
</script>

<script>
start_gl('mainCanvas', document.getElementById('my_vertex_shader'  ).innerHTML,
                    document.getElementById('my_fragment_shader').innerHTML);
</script>

</body>
</html>